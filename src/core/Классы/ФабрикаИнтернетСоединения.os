&Деталька("proxy.use")
Перем ИспользоватьПрокси;

&Деталька("proxy.server")
Перем ПроксиСервер;

&Деталька("proxy.port")
Перем ПроксиПорт;

&Деталька("proxy.user")
Перем ПроксиПользователь;

&Деталька("proxy.password")
Перем ПроксиПароль;

&Деталька("proxy.osAuthentication")
Перем ПроксиАвторизацияОС;

&Пластилин
Перем АдресСайтаОСкрипт;

&ЛогOVM
Перем Лог;

&Желудь
Процедура ПриСозданииОбъекта()
КонецПроцедуры

Функция Создать() Экспорт
	Если ИспользоватьПрокси = "true" Тогда

		Если Не ЗначениеЗаполнено(ПроксиСервер) Тогда

			Лог.Отладка("Использую системный прокси");
			Прокси = Новый ИнтернетПрокси(Истина);

		ИначеЕсли ЗначениеЗаполнено(ПроксиСервер) Тогда

			Лог.Отладка("Использую прокси %1", ПроксиСервер);
			Прокси = Новый ИнтернетПрокси();

			Прокси.Установить("http",
				ПроксиСервер,
				ПроксиПорт,
				ПроксиПользователь,
				ПроксиПароль,
				ПроксиАвторизацияОС
			);

			Прокси.Установить("https",
				ПроксиСервер,
				ПроксиПорт,
				ПроксиПользователь,
				ПроксиПароль,
				ПроксиАвторизацияОС
			);

		КонецЕсли;
	Иначе
		Лог.Отладка("Прокси не используется");
		Прокси = Неопределено;
	КонецЕсли;

	Таймаут = 10;
	Соединение = Новый HTTPСоединение(
		АдресСайтаОСкрипт,
		,
		,
		,
		Прокси,
		Таймаут
	);

	Возврат Соединение;
КонецФункции