#Использовать fs
#Использовать logos

Перем ЗначенияНастроек;
Перем Индекс;
Перем ПутьКФайлу;

Перем Лог;

&Желудь
Процедура ПриСозданииОбъекта(&ЛогOVM пЛог, &Пластилин ПараметрыOVM)

	Лог = пЛог;

	ПутьКФайлу = ОбъединитьПути(ПараметрыOVM.КаталогУстановкиПоУмолчанию(), "autumn-properties.ini");

	ПрочитатьНастройки();

КонецПроцедуры

Процедура Установить(Знач Имя, Знач Значение) Экспорт

	РегулярноеВыражение = Новый РегулярноеВыражение("[A-Za-z0-9\.]+");
	РегулярноеВыражение.Многострочный = Ложь;
	Если Не РегулярноеВыражение.Совпадает(Имя) Тогда
		ВызватьИсключение "Некорректно задано имя параметра";
	КонецЕсли;

	НомерСтроки = Индекс[Имя];
	Если НомерСтроки = Неопределено Тогда
		НомерСтроки = ЗначенияНастроек.Количество();
		Индекс[Имя] = НомерСтроки;
		СтрокаНастроек = ЗначенияНастроек.Добавить();
		СтрокаНастроек.Имя = Имя;
	КонецЕсли;

	ЗначенияНастроек[НомерСтроки].Значение = Значение;
КонецПроцедуры

Процедура Записать() Экспорт

	ФайлОпций = Новый Файл(ПутьКФайлу);
	ФС.ОбеспечитьКаталог(ФайлОпций.Путь);

	ТД = Новый ТекстовыйДокумент();
	Для Каждого Настройка Из ЗначенияНастроек Цикл

		Если ЗначениеЗаполнено(Настройка.Имя) Тогда
			ТД.ДобавитьСтроку(СтрШаблон("%1=%2", Настройка.Имя, Настройка.Значение));
		Иначе
			ТД.ДобавитьСтроку(Настройка.Содержимое);
		КонецЕсли;
	КонецЦикла;

	ТД.Записать(ПутьКФайлу);

КонецПроцедуры

// ------------------------------------------------------
Процедура ПрочитатьНастройки()
	
	Если Не ФС.ФайлСуществует(ПутьКФайлу) Тогда
		Лог.Отладка("Отсутствует файл настроек %1", ПутьКФайлу);
		Возврат;
	КонецЕсли;

	ТД = Новый ТекстовыйДокумент();
	ТД.Прочитать(ПутьКФайлу);

	Лог.Отладка("Прочитан файл настроек %1", ПутьКФайлу);
	Для Сч = 1 По ТД.КоличествоСтрок() Цикл
		СтрокаНастроек = ТД.ПолучитьСтроку(Сч);
		Если ПустаяСтрока(СтрокаНастроек) или Лев(СтрокаНастроек, 1) = "#" Тогда
			ДобавитьНеактивнуюСтроку(СтрокаНастроек);
			Продолжить;
		КонецЕсли;

		Поз = СтрНайти(СтрокаНастроек, "=");
		Если Поз = 0 Тогда
			ДобавитьНеактивнуюСтроку(СтрокаНастроек);
			Продолжить;
		КонецЕсли;

		Ключ = СокрЛП(Лев(СтрокаНастроек, Поз - 1));
		Значение = СокрЛП(Сред(СтрокаНастроек, Поз + 1));

		Попытка
			Установить(Ключ, Значение);
		Исключение
			// сбойный ключ
			Лог.Ошибка("Ключ настроек %1 некорректен", Ключ);
			ДобавитьНеактивнуюСтроку(СтрокаНастроек);
		КонецПопытки;
	КонецЦикла;

КонецПроцедуры

Процедура ДобавитьНеактивнуюСтроку(Знач Содержимое)
	СтрокаНастроек = ЗначенияНастроек.Добавить();
	СтрокаНастроек.Содержимое = Содержимое;
КонецПроцедуры

// ------------------------------------------------------
ЗначенияНастроек = Новый ТаблицаЗначений();
ЗначенияНастроек.Колонки.Добавить("Имя");
ЗначенияНастроек.Колонки.Добавить("Значение");
ЗначенияНастроек.Колонки.Добавить("Содержимое");

Индекс = Новый Соответствие();