#Использовать cpuinfo

Перем СистемнаяИнформация;
Перем ТекущаяПлатформа;

&Табакерка
&Пластилин("Процессор")
Перем ПровайдерИнформацииОПроцессоре;

&Желудь
Процедура ПриСозданииОбъекта()
	СистемнаяИнформация = Новый СистемнаяИнформация();
	ТекущаяПлатформа = СистемнаяИнформация.ТипПлатформы;
КонецПроцедуры

Функция ЭтоWindows() Экспорт

	Возврат ТекущаяПлатформа = ТипПлатформы.Windows_x86_64
		Или ТекущаяПлатформа = ТипПлатформы.Windows_x86;

КонецФункции

Функция ЭтоLinux() Экспорт

	Возврат ТекущаяПлатформа = ТипПлатформы.Linux_x86_64
		Или ТекущаяПлатформа = ТипПлатформы.Linux_x86;

КонецФункции

Функция ЭтоMacOS() Экспорт

	Возврат ТекущаяПлатформа = ТипПлатформы.MacOS_x86_64
		Или ТекущаяПлатформа = ТипПлатформы.MacOS_x86;

КонецФункции

Функция ЭтоMacOSARM() Экспорт

	Если Не ЭтоMacOS() Тогда
		Возврат Ложь;
	КонецЕсли;

	АрхитектураПроцессора = ПровайдерИнформацииОПроцессоре
		.Достать()
		.Архитектура;

	Возврат АрхитектураПроцессора = АрхитектурыПроцессоров.ARM
		Или АрхитектураПроцессора = АрхитектурыПроцессоров.ARM64;

КонецФункции

Функция ПутьКПрофилюПользователя() Экспорт
	Возврат СистемнаяИнформация.ПолучитьПутьПапки(СпециальнаяПапка.ПрофильПользователя);
КонецФункции

Функция ПутьКЛокальнымДаннымПриложений() Экспорт
	Возврат СистемнаяИнформация.ПолучитьПутьПапки(СпециальнаяПапка.ЛокальныйКаталогДанныхПриложений);
КонецФункции

Функция ЭтоX64() Экспорт
	Возврат СистемнаяИнформация.Это64БитнаяОперационнаяСистема;
КонецФункции
