#Использовать logos

Перем СистемнаяИнформация;

&Деталька(Значение = "oscript.server", ЗначениеПоУмолчанию = "https://oscript.io")
Перем АдресСайтаОСкрипт;

&Дуб
Процедура ПриСозданииОбъекта()
КонецПроцедуры

// Путь к каталогу установки версий OneScript по умолчанию
//
//  Возвращаемое значение:
//   Строка - Путь к каталогу установки по умолчанию
//
&Завязь(Тип = "Строка")
Функция КаталогУстановкиПоУмолчанию() Экспорт

	КаталогУстановкиПоУмолчанию = ПолучитьПеременнуюСреды("OVM_INSTALL_PATH");
	Если НЕ ЗначениеЗаполнено(КаталогУстановкиПоУмолчанию) Тогда
		КаталогУстановкиПоУмолчанию = ОбъединитьПути(
			СистемнаяИнформация.ПолучитьПутьПапки(СпециальнаяПапка.ЛокальныйКаталогДанныхПриложений),
			"ovm"
		);
	КонецЕсли;

	Возврат КаталогУстановкиПоУмолчанию;

КонецФункции

// Адрес сайта OneScript
//
//  Возвращаемое значение:
//   Строка - Адрес сайта OneScript
//
&Завязь(Тип = "Строка")
Функция АдресСайтаОСкрипт() Экспорт
	Возврат АдресСайтаОСкрипт;
КонецФункции

// Полный адрес к каталогу дистрибутивов OneScript
//
//  Возвращаемое значение:
//   Строка - Полный адрес к каталогу дистрибутивов OneScript
//
&Завязь(Тип = "Строка")
Функция ПолныйАдресККаталогуДистрибутивов() Экспорт
	Возврат АдресСайтаОСкрипт() + "/downloads";
КонецФункции

// Разрядность текущей операционной системы
//
//  Возвращаемое значение:
//   Булево - это 64 битная операционна система
&Завязь(Тип = "Булево")
Функция Это64БитнаяОперационнаяСистема() Экспорт
	Возврат СистемнаяИнформация.Это64БитнаяОперационнаяСистема;
КонецФункции

СистемнаяИнформация = Новый СистемнаяИнформация;
