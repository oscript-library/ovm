#Использовать logos

Перем СистемнаяИнформация;
Перем Лог;
Перем ОсновнойСпособВывода;

Перем ФайлНастроек;

// Путь к каталогу установки версий OneScript по умолчанию
//
//  Возвращаемое значение:
//   Строка - Путь к каталогу установки по умолчанию
//
Функция КаталогУстановкиПоУмолчанию() Экспорт
	
	КаталогУстановкиПоУмолчанию = ПолучитьПеременнуюСреды("OVM_INSTALL_PATH");
	Если НЕ ЗначениеЗаполнено(КаталогУстановкиПоУмолчанию) Тогда
		КаталогУстановкиПоУмолчанию = ОбъединитьПути(
			СистемнаяИнформация.ПолучитьПутьПапки(СпециальнаяПапка.ЛокальныйКаталогДанныхПриложений),
			"ovm"
		);
	КонецЕсли;
	
	Возврат КаталогУстановкиПоУмолчанию;
	
КонецФункции

// Адрес сайта OneScript
//
//  Возвращаемое значение:
//   Строка - Адрес сайта OneScript
//
Функция АдресСайтаОСкрипт() Экспорт
	Возврат ЗначениеНастройки("oscript.server", "https://oscript.io");
КонецФункции

// Полный адрес к каталогу дистрибутивов OneScript
//
//  Возвращаемое значение:
//   Строка - Полный адрес к каталогу дистрибутивов OneScript
//
Функция ПолныйАдресККаталогуДистрибутивов() Экспорт
	Возврат АдресСайтаОСкрипт() + "/downloads";
КонецФункции

// Разрядность текущей операционной системы
//
//  Возвращаемое значение:
//   Булево - это 64 битная операционна система
Функция Это64БитнаяОперационнаяСистема() Экспорт
	Возврат СистемнаяИнформация.Это64БитнаяОперационнаяСистема;
КонецФункции

// Количество разрядов в номере версии.
// В приложении используется версионирование вида 1.0.20
//
//  Возвращаемое значение:
//   Число - Количество разрядов в номере версии
//
Функция КоличествоРазрядовВНомереВерсии() Экспорт
	Возврат 3;
КонецФункции

// Получить основной лог приложения
//
//  Возвращаемое значение:
//   Лог - Основной лог приложения
//
Функция ПолучитьЛог() Экспорт
	Возврат Лог;
КонецФункции

// Получить основной способ вывода
//
//  Возвращаемое значение:
//   СпособВывода - Способ вывода фреймворка логирования logos
//
Функция ПолучитьОсновнойСпособВывода() Экспорт
	Возврат ОсновнойСпособВывода;
КонецФункции

// Форматирование строки, выводимой в лог (раскладка).
//
// Параметры:
//   Уровень - УровниЛога - Уровень логирования выводимого сообщения
//   Сообщение - Строка - Выводимое сообщение
//
//  Возвращаемое значение:
//   Строка - Сообщение, отформатированное с учетом раскладки
//
Функция Форматировать(Знач Уровень, Знач Сообщение) Экспорт
	
	Если Уровень = УровниЛога.Отладка ИЛИ Уровень = УровниЛога.Ошибка Тогда
		Сообщение = СтрШаблон("%1 - %2", УровниЛога.НаименованиеУровня(Уровень), Сообщение);
	КонецЕсли;
	
	Возврат Сообщение;
	
КонецФункции

//////////////////////////////////////////////////////////////////////

Функция ЗначениеНастройки(Знач Ключ, Знач ЗначениеПоУмолчанию = Неопределено) Экспорт
	Опция = ФайлНастроек.Получить(Ключ);
	Если Опция = Неопределено Тогда
		Возврат ЗначениеПоУмолчанию;
	КонецЕсли;

	Возврат Опция;

КонецФункции

Функция ХранилищеНастроек() Экспорт
	Возврат ФайлНастроек;
КонецФункции

//////////////////////////////////////////////////////////////////////

СистемнаяИнформация = Новый СистемнаяИнформация;
ОсновнойСпособВывода = ЛогосАппендер;

Лог = Логирование.ПолучитьЛог("oscript.app.ovm");
Лог.УстановитьРаскладку(ЭтотОбъект);
Лог.ДобавитьСпособВывода(ОсновнойСпособВывода);

ИмяФайлаНастроек = ОбъединитьПути(КаталогУстановкиПоУмолчанию(), "ovm.config");
ФайлНастроек = Новый ФайлНастроек(ИмяФайлаНастроек);
