#Использовать "../../core"

&ЛогOVM
Перем Лог;

&Пластилин
Перем УстановщикOneScript;

&Пластилин("ДетекторОкружения")
Перем Окружение;

&КомандаПриложения(Имя = "install i", Описание = "Установить OneScript указанных версий")
Процедура ПриСозданииОбъекта()
КонецПроцедуры

// Заполняет описание команды для библиотеки cli
//
// Параметры:
//   КомандаПриложения - КомандаПриложения - Настраиваемая команда
//
Процедура ОписаниеКоманды(КомандаПриложения) Экспорт
	
	КомандаПриложения.Опция("name", "", "Синоним (алиас) устанавливаемой версии для последующего использования в ovm use")
		.ТСтрока()
		.ВОкружении("OVM_INSTALL_NAME");

	КомандаПриложения.Опция("clean c", Ложь, "Полностью очищать каталог установки (включая установленные библиотеки)")
		.ВОкружении("OVM_INSTALL_CLEAN");
	
	Если Окружение.ЭтоX64() Тогда
		КомандаПриложения.Опция("x86", Ложь, "Устанавливать 32-разрядный дистрибутив OneScript")
			.ВОкружении("OVM_INSTALL_X86");
	КонецЕсли;

	КомандаПриложения.Опция("fdd", Ложь, "Устанавливать fdd дистрибутив OneScript (требует установленный .NET для работы)")
		.ВОкружении("OVM_INSTALL_FDD");

	КомандаПриложения.Аргумент(
		"VERSION",
		,
		"Устанавливаемая версия (версии) OneScript. " + 
			"Допустимо использовать трехномерные версии (1.0.17, 1.0.18), " +
			"lts, stable, dev, preview, lts-dev " +
			"Может быть передано несколько значений")
		.ТМассивСтрок()
		.ВОкружении("OVM_INSTALL_VERSION");
	
КонецПроцедуры

// Обработчик выполнения команды
//
// Параметры:
//   КомандаПриложения - КомандаПриложения - Выполняемая команда
//
Процедура ВыполнитьКоманду(Знач КомандаПриложения) Экспорт

	МассивВерсийКУстановке = КомандаПриложения.ЗначениеАргумента("VERSION");
	АлиасВерсии = КомандаПриложения.ЗначениеОпции("name");

	ДополнительныеПараметры = УстановщикOneScript.ДополнительныеПараметрыУстановки();

	ДополнительныеПараметры.ОчищатьКаталогУстановки = КомандаПриложения.ЗначениеОпции("clean");

	Если ЗначениеЗаполнено(АлиасВерсии) И МассивВерсийКУстановке.Количество() > 1 Тогда
		ВызватьИсключение "Опция <--name> может быть задана только при установке одной версии OneScript";
	КонецЕсли;

	Если Окружение.ЭтоX64() Тогда
		ДополнительныеПараметры.ИспользоватьХ64 = НЕ КомандаПриложения.ЗначениеОпции("x86");
	Иначе
		ДополнительныеПараметры.ИспользоватьХ64 = Ложь;
	КонецЕсли;

	ДополнительныеПараметры.ИспользоватьFDD = КомандаПриложения.ЗначениеОпции("fdd");

	Для Каждого ВерсияКУстановке Из МассивВерсийКУстановке Цикл
		УстановщикOneScript.УстановитьOneScript(ВерсияКУстановке, АлиасВерсии, ДополнительныеПараметры);
	КонецЦикла;

	Если МассивВерсийКУстановке.Количество() > 0 Тогда
		Лог.Информация(
			"Для начала использования версии OneScript, выполните команду:
			|ovm use %1",
			?(ЗначениеЗаполнено(АлиасВерсии), АлиасВерсии, МассивВерсийКУстановке[МассивВерсийКУстановке.ВГраница()])
		);
	КонецЕсли;

КонецПроцедуры
