&ЛогOVM
Перем Лог;

&Пластилин
Перем ДеинсталляторOneScript;

&Пластилин
Перем ВерсииOneScript;

&Аргумент(
	Имя = "VERSION",
	Описание = "Удаляемая версия (версии) OneScript. Допустимо использовать трехномерные версии (1.0.17, 1.0.18), stable, dev. Может быть передано несколько значений"
)
&ТМассивСтрок
&ВОкружении("OVM_UNINSTALL_VERSION")
Перем МассивВерсийКУдалению;

&Опция(Имя = "force f", Описание = "Удалять даже текущую используемую версию")
&ПоУмолчанию(Ложь)
&ВОкружении("OVM_UNINSTALL_FORCE")
Перем УдалятьТекущуюВерсию;

&Опция(Имя = "all a", Описание = "Удаление всех установленных версий OneScript")
&ПоУмолчанию(Ложь)
&ВОкружении("OVM_UNINSTALL_ALL")
Перем УдалятьВсеВерсии;

&КомандаПриложения(Имя = "uninstall delete d", Описание = "Удалить OneScript указанных версий")
Процедура ПриСозданииОбъекта()
КонецПроцедуры

&ВыполнениеКоманды
Процедура ВыполнениеКоманды() Экспорт

	Если УдалятьВсеВерсии Тогда
		ДеинсталляторOneScript.УдалитьВсеВерсииOneScript();
	Иначе

		Для Каждого ВерсияКУдалению Из МассивВерсийКУдалению Цикл
			Если ВерсииOneScript.ЭтоТекущаяВерсия(ВерсияКУдалению) И НЕ УдалятьТекущуюВерсию Тогда
				Лог.Информация("Версия <%1> не удалена, т.к. является текущей.", ВерсияКУдалению);
				Продолжить;
			КонецЕсли;
			ДеинсталляторOneScript.УдалитьOneScript(ВерсияКУдалению);
		КонецЦикла;

	КонецЕсли;

КонецПроцедуры
