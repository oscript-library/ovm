#Использовать "../../core"

Перем Лог;

// Заполняет описание команды для библиотеки cli
//
// Параметры:
//   КомандаПриложения - КомандаПриложения - Настраиваемая команда
//
Процедура ОписаниеКоманды(КомандаПриложения) Экспорт
	
	КомандаПриложения
		.Опция("force f", Ложь, "Удалять даже текущую используемую версию")
		.ВОкружении("OVM_UNINSTALL_FORCE");
	
	КомандаПриложения
		.Опция("all a", Ложь, "Удаление всех установленных версий OneScript")
		.ВОкружении("OVM_UNINSTALL_ALL");
	
	КомандаПриложения
		.Аргумент(
			"VERSION",
			,
			"Удаляемая версия (версии) OneScript. " +
				"Допустимо использовать трехномерные версии (1.0.17, 1.0.18), stable, dev. " +
				"Может быть передано несколько значений")
		.ВОкружении("OVM_UNINSTALL_VERSION")
		.ТМассивСтрок();
		
	КомандаПриложения.Спек = "[--force] [--all] [VERSION...]";
КонецПроцедуры

// Обработчик выполнения команды
//
// Параметры:
//   КомандаПриложения - КомандаПриложения - Выполняемая команда
//
Процедура ВыполнитьКоманду(Знач КомандаПриложения) Экспорт
	
	МассивВерсийКУдалению = КомандаПриложения.ЗначениеАргумента("VERSION");
	УдалятьТекущуюВерсию = КомандаПриложения.ЗначениеОпции("force");
	УдалятьВсеВерсии = КомандаПриложения.ЗначениеОпции("all");

	ДеинсталляторOneScript = Новый ДеинсталляторOneScript();
	
	Если УдалятьВсеВерсии Тогда
		ДеинсталляторOneScript.УдалитьВсеВерсииOneScript();
	Иначе
			
		Для Каждого ВерсияКУдалению Из МассивВерсийКУдалению Цикл
			Если ВерсииOneScript.ЭтоТекущаяВерсия(ВерсияКУдалению) И НЕ УдалятьТекущуюВерсию Тогда
				Лог.Информация("Версия <%1> не удалена, т.к. является текущей.", ВерсияКУдалению);
				Продолжить;
			КонецЕсли;
			ДеинсталляторOneScript.УдалитьOneScript(ВерсияКУдалению);
		КонецЦикла;
		
	КонецЕсли;
		
КонецПроцедуры

Лог = ПараметрыOVM.ПолучитьЛог();
