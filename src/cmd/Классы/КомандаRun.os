#Использовать "../../core"

&Пластилин
Перем ПараметрыOVM;

&Аргумент(
	Имя = "VERSION",
	Описание = "Используемое окружение OneScript. Допустимо использовать трехномерные версии (1.0.17, 1.0.18), stable, dev, current"
)
&ПоУмолчанию("current")
&ВОкружении("OVM_RUN_VERSION")
Перем ИспользуемаяВерсия;

&Аргумент(Имя = "APP", Описание = "Используемое приложение. Например, oscript, opm, vrunner...")
&ПоУмолчанию("oscript")
&ВОкружении("OVM_RUN_APP")
Перем Приложение;

&Аргумент(Имя = "ARGS", Описание = "Аргументы, передаваемые приложению")
&ПоУмолчанию("")
&ТМассивСтрок
&Обязательный(Ложь)
&ВОкружении("OVM_RUN_ARGS")
Перем Аргументы;

&Пластилин("ДетекторОкружения")
Перем Окружение;

&КомандаПриложения(Имя = "run r", Описание = "Запустить исполняемый файл в окружении указанной версии OneScript")
Процедура ПриСозданииОбъекта()
КонецПроцедуры

&ВыполнениеКоманды
Процедура ВыполнениеКоманды() Экспорт

	КаталогУстановки = ПараметрыOVM.КаталогУстановкиПоУмолчанию();
	КаталогУстановкиВерсии = ОбъединитьПути(КаталогУстановки, ИспользуемаяВерсия);
	КаталогBin = ОбъединитьПути(КаталогУстановкиВерсии, "bin");

	ПеременнаяPATH = ПолучитьПеременнуюСреды("PATH");
	Если Окружение.ЭтоWindows() Тогда
		ПеременнаяPATH = КаталогBin + ";" + ПеременнаяPATH;
	Иначе
		ПеременнаяPATH = КаталогBin + ":" + ПеременнаяPATH;
	КонецЕсли;

	УстановитьПеременнуюСреды("PATH", ПеременнаяPATH, РасположениеПеременнойСреды.Процесс);

	Команда = Новый Команда;
	Команда.УстановитьИсполнениеЧерезКомандыСистемы(Ложь);
	Команда.УстановитьКоманду(Приложение);
	Команда.ДобавитьПараметры(Аргументы);
	Команда.ПоказыватьВыводНемедленно(Истина);

	Команда.Исполнить();

КонецПроцедуры
