/////////////////////////////////////////////////////
// Тесты выбора дистрибутива в перечне файлов
/////////////////////////////////////////////////////

#Использовать "../src/cmd"
#Использовать "../src/core"
#Использовать moskito
#Использовать cpuinfo

Перем Арх32;
Перем Арх64;

// BSLLS:DuplicateStringLiteral-off

&ПараметризованныйТест
&ИсточникЗначение(Ложь, Ложь, Неопределено)
&ИсточникЗначение(Ложь, Истина, Неопределено)
&ИсточникЗначение(Истина, Ложь, Неопределено)
&ИсточникЗначение(Истина, Истина, Неопределено)
&ИсточникЗначение(Ложь, Ложь, Истина)
&ИсточникЗначение(Ложь, Истина, Истина)
&ИсточникЗначение(Истина, Ложь, Истина)
&ИсточникЗначение(Истина, Истина, Истина)
Процедура ТестУстановкиВерсии2Windows(ИспользоватьFDD, ИспользоватьХ64, ЭтоВерсия2) Экспорт
	
	УниверсальныйТестВерсии(
		ПереченьФайловВерсии2(),
		ИспользоватьFDD,
		ИспользоватьХ64,
		ЭтоВерсия2,
		ТипПлатформы.Windows_x86_64,
		"scd-win");

КонецПроцедуры

&ПараметризованныйТест
&ИсточникЗначение(Ложь, Ложь, Неопределено)
&ИсточникЗначение(Ложь, Истина, Неопределено)
&ИсточникЗначение(Истина, Ложь, Неопределено)
&ИсточникЗначение(Истина, Истина, Неопределено)
&ИсточникЗначение(Ложь, Ложь, Истина)
&ИсточникЗначение(Ложь, Истина, Истина)
&ИсточникЗначение(Истина, Ложь, Истина)
&ИсточникЗначение(Истина, Истина, Истина)
Процедура ТестУстановкиВерсии2Linux(ИспользоватьFDD, ИспользоватьХ64, ЭтоВерсия2) Экспорт
	
	Если ИспользоватьХ64 = Ложь И ИспользоватьFDD = Ложь Тогда // На линуксе нет варианта x86
		ТекстИсключения = ?(ЭтоВерсия2 = Неопределено, "Вид=zip", "Вид=scd-lin");
	Иначе
		ТекстИсключения = Неопределено;
	КонецЕсли;

	УниверсальныйТестВерсии(
		ПереченьФайловВерсии2(),
		ИспользоватьFDD,
		ИспользоватьХ64,
		ЭтоВерсия2,
		ТипПлатформы.Linux_x86_64,
		"scd-lin",
		ТекстИсключения);

КонецПроцедуры

&ПараметризованныйТест
&ИсточникЗначение(Ложь, Ложь, Неопределено)
&ИсточникЗначение(Ложь, Истина, Неопределено)
&ИсточникЗначение(Истина, Ложь, Неопределено)
&ИсточникЗначение(Истина, Истина, Неопределено)
&ИсточникЗначение(Ложь, Ложь, Истина)
&ИсточникЗначение(Ложь, Истина, Истина)
&ИсточникЗначение(Истина, Ложь, Истина)
&ИсточникЗначение(Истина, Истина, Истина)
Процедура ТестУстановкиВерсии2Mac(ИспользоватьFDD, ИспользоватьХ64, ЭтоВерсия2) Экспорт
	
	Если ИспользоватьХ64 = Ложь И ИспользоватьFDD = Ложь Тогда // На маке нет варианта x86
		ТекстИсключения = ?(ЭтоВерсия2 = Неопределено, "Вид=zip", "Вид=osx-x64");
	Иначе
		ТекстИсключения = Неопределено;
	КонецЕсли;

	УниверсальныйТестВерсии(
		ПереченьФайловВерсии2(),
		ИспользоватьFDD,
		ИспользоватьХ64,
		ЭтоВерсия2,
		ТипПлатформы.MacOs_x86_64,
		"osx-x64",
		ТекстИсключения);

КонецПроцедуры

&ПараметризованныйТест
&ИсточникЗначение(Ложь, Ложь, Неопределено)
&ИсточникЗначение(Ложь, Истина, Неопределено)
&ИсточникЗначение(Истина, Ложь, Неопределено)
&ИсточникЗначение(Истина, Истина, Неопределено)
&ИсточникЗначение(Ложь, Ложь, Истина)
&ИсточникЗначение(Ложь, Истина, Истина)
&ИсточникЗначение(Истина, Ложь, Истина)
&ИсточникЗначение(Истина, Истина, Истина)
Процедура ТестУстановкиВерсии2MacARM(ИспользоватьFDD, ИспользоватьХ64, ЭтоВерсия2) Экспорт
	
	Если ИспользоватьХ64 = Ложь И ИспользоватьFDD = Ложь Тогда // На маке нет варианта x86
		ТекстИсключения = ?(ЭтоВерсия2 = Неопределено, "Вид=zip", "Вид=osx-arm64");
	Иначе
		ТекстИсключения = Неопределено;
	КонецЕсли;

	УниверсальныйТестВерсии(
		ПереченьФайловВерсии2(),
		ИспользоватьFDD,
		ИспользоватьХ64,
		ЭтоВерсия2,
		ТипПлатформы.MacOs_x86_64,
		"osx-arm64",
		ТекстИсключения,
		АрхитектурыПроцессоров.ARM64);

КонецПроцедуры

&ПараметризованныйТест
&ИсточникЗначение(Ложь, Ложь, Неопределено)
&ИсточникЗначение(Ложь, Истина, Неопределено)
&ИсточникЗначение(Истина, Ложь, Неопределено)
&ИсточникЗначение(Истина, Истина, Неопределено)
&ИсточникЗначение(Ложь, Ложь, Ложь)
&ИсточникЗначение(Ложь, Истина, Ложь)
&ИсточникЗначение(Истина, Ложь, Ложь)
&ИсточникЗначение(Истина, Истина, Ложь)
Процедура ТестУстановкиВерсии1Windows(ИспользоватьFDD, ИспользоватьХ64, ЭтоВерсия2) Экспорт
	Если ИспользоватьFDD Тогда // На версии 1 нет варианта fdd
		ТекстИсключения = ?(ЭтоВерсия2 = Неопределено, "Вид=fdd", Неопределено);
	Иначе
		ТекстИсключения = Неопределено;
	КонецЕсли;

	УниверсальныйТестВерсии(
		ПереченьФайловВерсии1(),
		ИспользоватьFDD,
		ИспользоватьХ64,
		ЭтоВерсия2,
		ТипПлатформы.Windows_x86_64,
		"zip",
		ТекстИсключения);

КонецПроцедуры

&ПараметризованныйТест
&ИсточникЗначение(Ложь, Ложь, Неопределено)
&ИсточникЗначение(Ложь, Истина, Неопределено)
&ИсточникЗначение(Истина, Ложь, Неопределено)
&ИсточникЗначение(Истина, Истина, Неопределено)
&ИсточникЗначение(Ложь, Ложь, Ложь)
&ИсточникЗначение(Ложь, Истина, Ложь)
&ИсточникЗначение(Истина, Ложь, Ложь)
&ИсточникЗначение(Истина, Истина, Ложь)
Процедура ТестУстановкиВерсии1Linux(ИспользоватьFDD, ИспользоватьХ64, ЭтоВерсия2) Экспорт
	Если ИспользоватьFDD Тогда // На версии 1 нет варианта fdd
		ТекстИсключения = ?(ЭтоВерсия2 = Неопределено, "Вид=fdd", Неопределено);
	Иначе
		ТекстИсключения = Неопределено;
	КонецЕсли;

	УниверсальныйТестВерсии(
		ПереченьФайловВерсии1(),
		ИспользоватьFDD,
		ИспользоватьХ64,
		ЭтоВерсия2,
		ТипПлатформы.Linux_x86_64,
		"zip",
		ТекстИсключения);

КонецПроцедуры

// BSLLS:NumberOfParams-off
Процедура УниверсальныйТестВерсии(
	ПереченьФайлов,
	ИспользоватьFDD,
	ИспользоватьХ64,
	ЭтоВерсия2,
	Платформа,
	ОжидаемыйДистрибутив,
	ТекстИсключения = Неопределено,
	Архитектура = Неопределено)
	
	СисИнфо = МокОкружения(Платформа, Архитектура);
	Определитель = Новый ОпределительДистрибутива(СисИнфо);

	Определитель
		.ИспользоватьFDD(ИспользоватьFDD)
		.ИспользоватьХ64(ИспользоватьХ64)
		.ЭтоДистрибутивНеткор(ЭтоВерсия2);

	Вид = ?(ИспользоватьFDD, "fdd", ОжидаемыйДистрибутив);
	Арх = ?(ИспользоватьХ64, Арх64, Арх32);

	Если ИспользоватьFDD и ОжидаемыйДистрибутив = "zip" Тогда
		// Для версии 1 ключ fdd не должен влиять на результат
		// т.к. все версии 1 являются "fdd" (framework dependent) 
		Вид = "zip";
	КонецЕсли;

	Если ЗначениеЗаполнено(ТекстИсключения) Тогда
		ПараметрыДействия = Новый Массив;
		ПараметрыДействия.Добавить(ПереченьФайлов);
		Ожидаем.Что(Определитель).Метод("Выбрать", ПараметрыДействия)
			.ВыбрасываетИсключение("Не найден дистрибутив для критериев: " + ТекстИсключения);
	Иначе
		Ссылка = Определитель.Выбрать(ПереченьФайлов);
		Ожидаем.Что(Ссылка).Равно(Вид + "/" + Арх);
	КонецЕсли;

КонецПроцедуры

Функция ПереченьФайловВерсии1()
	
	Таблица = ТаблицаФайловДистрибутивов();
	ЗаписьДистрибутива(Таблица, "vsix", Арх32);
	ЗаписьДистрибутива(Таблица, "exe", Арх32);
	ЗаписьДистрибутива(Таблица, "exe", Арх64);
	ЗаписьДистрибутива(Таблица, "zip", Арх32);
	ЗаписьДистрибутива(Таблица, "zip", Арх64);
	ЗаписьДистрибутива(Таблица, "rpm", Арх64);
	ЗаписьДистрибутива(Таблица, "deb", Арх64);

	Возврат Таблица;

КонецФункции

Функция ПереченьФайловВерсии2()
	
	Таблица = ТаблицаФайловДистрибутивов();
	ЗаписьДистрибутива(Таблица, "vsix", Арх32);
	ЗаписьДистрибутива(Таблица, "fdd", Арх32);
	ЗаписьДистрибутива(Таблица, "fdd", Арх64);
	ЗаписьДистрибутива(Таблица, "scd-win", Арх32);
	ЗаписьДистрибутива(Таблица, "scd-win", Арх64);
	ЗаписьДистрибутива(Таблица, "scd-lin", Арх64);
	ЗаписьДистрибутива(Таблица, "osx-x64", Арх64);
	ЗаписьДистрибутива(Таблица, "osx-arm64", Арх64);

	Возврат Таблица;

КонецФункции

Функция МокОкружения(Знач ВидПлатформы, Знач Архитектура)
	Если Архитектура = Неопределено Тогда
		Архитектура = АрхитектурыПроцессоров.X64;
	КонецЕсли;

	МокСИ = Мок.Получить(Новый ДетекторОкружения);
	МокСИ.Когда().ТипПлатформы().ТогдаВозвращает(ВидПлатформы);
	МокСИ.Когда().АрхитектураПроцессора().ТогдаВозвращает(Архитектура);
	
	Возврат МокСИ;

КонецФункции

Процедура ЗаписьДистрибутива(Знач Таблица, Знач Вид, Знач Архитектура, Знач Ссылка = Неопределено, Знач ИмяФайла = Неопределено)
	
	Стр = Таблица.Добавить();
	Стр.Вид = Вид;
	Стр.Архитектура = Архитектура;
	Если Стр.Ссылка = Неопределено Тогда
		Стр.Ссылка = Вид + "/" + Архитектура;
	КонецЕсли;
	Стр.ИмяФайла = ИмяФайла;

КонецПроцедуры

Функция ТаблицаФайловДистрибутивов()
	ТаблицаРезультата = Новый ТаблицаЗначений();
	ТаблицаРезультата.Колонки.Добавить("Вид");
	ТаблицаРезультата.Колонки.Добавить("ИмяФайла");
	ТаблицаРезультата.Колонки.Добавить("Архитектура");
	ТаблицаРезультата.Колонки.Добавить("Ссылка");

	Возврат ТаблицаРезультата;
КонецФункции

///////////////////////////
Арх32 = "x86";
Арх64 = "x64";